##
##  Completion
##

# context string: ':completion:[function]:[completer]:[command]:[arg]' [tag]

# menu selection
#zstyle ':completion:*' menu select=10

# Colorize completion
_dircolors=$(which dircolors) || _dircolors=$(which gdircolors)
if [[ -x ${_dircolors} ]]; then
    if [[ -f ~/.dir_colors ]]; then 
        eval $(${_dircolors} -b ~/.dir_colors)
    else
        eval $(${_dircolors} -b)
    fi
    zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
fi

# prevent CVS files/directories from being completed
zstyle ':completion:*:(all-|)files' ignored-patterns '(|*/)CVS'
zstyle ':completion:*:cd:*' ignored-patterns '(*/)#CVS'

# Ignore completion functions for unknown commands:
zstyle ':completion:*:functions' ignored-patterns '_*'

# Process completion
#zstyle ':completion:*:*:*:*:processes' menu yes select
#zstyle ':completion:*:*:*:*:processes' force-list always
zstyle ':completion:*:processes-names' command 'ps -e -o comm='
zstyle ':completion:*:processes' command 'ps -au$USER'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;32'

# SSH known hosts
if [[ -f ${HOME}/.ssh/known_hosts ]] ; then
	zstyle ':completion:*' hosts ${${${(f)"$(<$HOME/.ssh/known_hosts)"}%%\ *}%%,*}
fi

# Quickly change to parent directories
rationalise-dot() {
  if [[ $LBUFFER = *.. ]]; then
    LBUFFER+=/..
  else
    LBUFFER+=.
  fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

# vim: ft=zsh
